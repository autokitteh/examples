# vim: set syntax=python:

load('os', 'os')
load('encoding/json', 'json')


def workspace(workdir=None, tfpath=None, default_env={}):
  if not tfpath:
    tfpath = os.look_path('terraform')

    if not tfpath:
      fail('unable to locate terraform')

  if workdir == None:
    workdir = os.make_temp_dir()

  def exec(cmd, args=[], vars={}, env=[], allow_fail=False):
    vargs = []
    if vars:
      # TODO: write to a tempfile somewhere, do not mess with workdir.
      path = '%s/terraform.tsvars.json' % workdir

      os.write_text_file(path, json.encode(vars))

      vargs = ['-var-file=' + path]

    if not env:
      env = default_env

    args = [cmd, '-no-color'] + vargs + args

    print("$ %s %s" % (tfpath, args))

    rc, out = os.exec(tfpath, args, env=env, dir=workdir, fail=not allow_fail)
    if rc != 0 and not allow_fail:
      fail("exec %s: rc=%d, out=%s" % (cmd, rc, out))

    print(out)

    return out

  def init(env=[]):
    exec('init', env=env)

  def import_(addr, id, vars={}, env=[], allow_fail=False):
    exec('import', ['-input=false'] + [addr, id], vars=vars, env=env, allow_fail=allow_fail)

  def apply(vars={}, env=[]):
    exec('apply', ['-input=false', '-auto-approve'], vars=vars, env=env)

  def destroy(vars={}, env=[]):
    exec('destroy', ['-input=false', '-auto-approve'], vars=vars, env=env)

  def output(env=[]):
    return json.decode(exec('output', ['-json'], env))

  return struct(
    init=init,
    import_=import_,
    apply=apply,
    destroy=destroy,
    output=output,
    exec=exec,
    workdir=workdir,
  )
